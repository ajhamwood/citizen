-- Void

let Void : U                                        = (V : U) -> V;
let absurd : {A} -> Void -> A                       = \V. _;
let neg : U -> U                                    = \P. P -> Void;


-- Unit

let Unit : U                                        = (T : U) -> T -> T;
let tt : Unit                                       = \T t. t;


-- Booleans

let Bit : U                                         = (B : U) -> B -> B -> B;
let I : Bit                                         = \B i o. i;
let O : Bit                                         = \B i o. o;
let not : Bit -> Bit                                = \b B i o. b B o i;
let and : Bit -> Bit -> Bit                         = \x y B i o. x B (y B i o) (x B i o);
let or : Bit -> Bit -> Bit                          = \x y B i o. x B (x B i o) (y B i o);
let xor : Bit -> Bit -> Bit                         = \x y B i o. x B (y B o i) (y B i o);
let if : {R} -> Bit -> R -> R -> R                  = \p a b. p _ a b;
let carry : Bit -> Bit -> Bit -> Bit                = \a b c. or (and a b) (and c (xor a b));
let sum : Bit -> Bit -> Bit -> Bit                  = \a b c. xor (xor a b) c;


-- Nats

let Nat : U                                         = (N : U) -> (N -> N) -> N -> N;
let zero : Nat                                      = \N s z. z;
let suc : Nat -> Nat                                = \n N s z. s (n N s z);
let add : Nat -> Nat -> Nat                         = \a b N s z. a N s (b N s z);
let mul : Nat -> Nat -> Nat                         = \a b N s z. a N (b N s) z;
let pred : Nat -> Nat                               = \n. snd {Nat} (n _ (\p. pair (suc (fst p)) (fst p)) (pair zero zero));
let minus : Nat -> Nat -> Nat                       = \m n. n _ pred m;
let eqz : Nat -> Bit                                = \n. n _ (\z B i o. o) (\B i o. i);

let fold : {A} -> (Nat -> A -> A) -> A -> Nat -> A
    = \{A} reducer base n. snd {Nat} (n {Pair Nat A} (\p. pair (suc (fst p)) (p reducer)) (pair zero base));
let fact : Nat -> Nat
    = \n. fold {Nat} (\k acc. mul acc (suc k)) (suc zero) n;
-- fact (suc (suc (suc zero)))


-- Identity

let Wrap : U -> U                                   = \A. (W : U) -> (A -> W) -> W;
let wrap : {A} -> A -> Wrap A                       = \a W w. w a;
let unwrap : {A} -> Wrap A -> A                     = \w. w _ (\x. x);


-- Products

let Pair : U -> U -> U                              = \A B. (P : U) -> (A -> B -> P) -> P;
let pair : {A B} -> A -> B -> Pair A B              = \a b P p. p a b;
let fst : {A B} -> Pair A B -> A                    = \s. s _ (\a b. a);
let snd : {A B} -> Pair A B -> B                    = \s. s _ (\a b. b);


-- Maybe

let Maybe : U -> U                                  = \A. (M : U) -> M -> (A -> M) -> M;
let nothing : {A} -> Maybe A                        = \M n j. n;
let just : {A} -> A -> Maybe A                      = \a M n j. j a;
let maybeFunctor : Functor Maybe                    = \F. F (\fn ma M n j. ma M n (\a. j (fn a)));
-- maybeFunctor (\map. map not (just O))


-- Either

let Either : U -> U -> U                            = \A B. (E : U) -> (A -> E) -> (B -> E) -> E;
let left : {A B} -> A -> Either A B                 = \a E l r. l a;
let right : {A B} -> B -> Either A B                = \b E l r. r b;


-- Lists

let List : U -> U                                   = \A. (L : U) -> L -> (A -> L -> L) -> L;
let nil  : {A} -> List A                            = \L n c. n;
let cons : {A} -> A -> List A -> List A             = \a as L n c. c a (as L n c);
let concat : {A} -> List A -> List A -> List A      = \{A} la lb. la (List A) lb (\a as. cons {A} a as);


-- Non-empty list

let Nonempty : U -> U                               = \A. {N} -> (A -> N) -> (A -> N -> N) -> N;
let sing : {A} -> A -> Nonempty A                   = \a s c. s a;
let cons : {A} -> A -> Nonempty A -> Nonempty A     = \a as s c. c a (as s c);


-- Continuations

let Cont : U -> U -> U                              = \W A. (C : U) -> (((A -> W) -> W) -> C) -> C;
let cont : {W A} -> ((A -> W) -> W) -> Cont W A     = \f C c. c f;
let run : {W A} -> Cont W A -> (A -> W) -> W        = \c f. c _ (\k. k f);
let reset : {W} -> Cont W W -> W                    = \m. run m (\x. x);
let shift : {W A} -> ((A -> W) -> Cont W W) -> Cont W A = \e. cont (\k. reset (e k));


-- Dependent Nats

let DNat : Nat -> U                                 = \n. (DN : Nat -> U) -> ({m} -> DN m -> DN (suc m)) -> DN zero -> DN n;
let dz : DNat zero                                  = \DN s z. z;
let dsuc : {m : Nat} -> DNat m -> DNat (suc m)      = \n DN s z. s (n DN s z);
let initWord : {n} -> DNat n -> Tree {n} Bit        = \dn. dn (\n. Tree {n} Bit) (\acc. node acc acc) (leaf O);
-- initWord (dsuc (dsuc dz))


-- Length-indexed Vectors

let Vec : {n} -> U -> U                             = \{n} A. (V : Nat -> U) -> V zero -> ({m : Nat} -> A -> V m -> V (suc m)) -> V n;
let nil : {A} -> Vec {zero} A                       = \V n c. n;
let cons : {A n} -> A -> Vec {n} A -> Vec {suc n} A = \a as V n c. c a (as V n c);
let head : {A n} -> Vec {suc n} A -> A              = \{A} v. v (\_. A) _ (\x xs. x);
let tail : {A n} -> Vec {suc n} A -> Vec {n} A      = \{A}{n} v. v (\_. Vec {n} A) _ (\x xs. xs);
-- tail (cons I (cons O nil))


-- Height-indexed Binary Trees

let Tree : {n} -> U -> U                            = \{n} A. (T : Nat -> U) -> (A -> T zero) -> ({m} -> T m -> T m -> T (suc m)) -> T n;
let leaf : {A} -> A -> Tree {zero} A                = \a T l n. l a;
let node : {A n} -> Tree {n} A -> Tree {n} A -> Tree {suc n} A = \ta tb T l n. n (ta T l n) (tb T l n);
let divine : {A} -> Tree {zero} A -> A              = \{A} t. t (\_. A) (\a. a) _;
let lbranch : {A n} -> Tree {suc n} A -> Tree {n} A = \{A}{n} t. t (\_. Tree {n} A) _ (\ta tb. ta);
let rbranch : {A n} -> Tree {suc n} A -> Tree {n} A = \{A}{n} t. t (\_. Tree {n} A) _ (\ta tb. tb);
-- divine (leaf O)
-- lbranch (node (leaf O) (leaf I))

let treeFunctor : Functor Tree                      = \F. F (\fn ta T l n. ta T (\a. l (fn a)) n);
-- treeFunctor (\map. map not (initWord (dsuc (dsuc dz))))


-- Bush

let Bush : {n} -> U -> U
    = \{n} A. (B : Nat -> U) -> (A -> B zero) -> ({n} -> B (suc n)) ->
        ({n} -> B n -> B (suc (suc n)) -> B (suc n)) -> B n;
let bbase : {A} -> A -> Bush {zero} A               = \a B b n c. b a;
let bnil : {n A} -> Bush {suc n} A                  = \B b n c. n;
let bcons : {n A} -> Bush {n} A -> Bush {suc (suc n)} A -> Bush {suc n} A
    = \x xs B b n c. c (x B b n c) (xs B b n c);
let mapbush : {A B n} -> (A -> B) -> Bush {n} A -> Bush {n} B
    = \{A}{B} fn bush. bush (\n. Bush {n} B) (\x. bbase (fn x)) bnil bcons;

-- [ O,
--   [ I, [ I ] ],
--   [ [ O ] ] ]
let somebush : Bush Bit
    = bcons (bbase O) (bcons
            (bcons (bbase I) (bcons
                   (bcons (bbase I) bnil)
                   bnil)) (bcons
            (bcons (bcons (bbase O) bnil)
                   bnil)
            bnil));
            
-- mapbush not somebush


-- Decided / Decidable

let Dec : U -> U                                    = \P. Either P (neg P);
let Decidable : {A B} -> (A -> B -> U) -> U         = \{A}{B} p. (x : A) -> (y : B) -> Dec (p x y);


-- Monoid

let Monoid : U -> U                                 = \(M : U). ((neut : M) -> (comb : {A} -> A -> M -> M) -> M) -> M;
let maybeMonoid : {A} -> Monoid (Maybe A)           = \{Z} M. M nothing (\{Y} a _. just {Y} a);
-- maybeMonoid {Bit} (\neut comb. comb I neut)


-- Functors

let Functor : (U -> U) -> U                         = \(F : U -> U). {T} -> ((map : {A B} -> (A -> B) -> F A -> F B) -> F T) -> F T;
let listFunctor : Functor List                      = \F. F (\fn la L n c. la L n (\a as. c (fn a) as));
-- listFunctor (\map. map not (cons O (cons I nil)))


-- Applicatives

let App : (U -> U) -> U
    = \(F : U -> U). {T} -> ((pure : {A} -> A -> F A) ->
                     (ap : {A B} -> F (A -> B) -> F A -> F B) -> F T) -> F T;
let appFunctor : {F} -> App F -> Functor F          = \app F. F (\{A}{B} fn fa. app (\pure ap. ap {A}{B} (pure fn) fa));
let listApp : App List                              = \A. A (\x. cons x nil) (\fs xs. fs _ nil (\f bs. concat (listFunctor (\map. map f xs)) bs));
-- listApp (\pure ap. ap (cons not (cons (\x. x) nil)) (cons O (cons I nil)))


-- Monads

let Monad : (U -> U) -> U
    = \(M : U -> U). {T} -> ((return : {A} -> A -> M A) ->
                     (bind : {A B} -> M A -> (A -> M B) -> M B) -> M T) -> M T;
let contMonad : {W} -> Monad (Cont W)               = \M. M (\x. cont (\k. k x)) (\m f. cont (\k. run m (\v. run (f v) k)));

let mapM : {A B}{M : U -> U}{monad : Monad M} -> (A -> M B) -> List A -> M (List B)
    = (\{A}{B}{M}{monad} f as. monad
        (\return bind. as (M (List B)) (return nil)
            (\a r. bind {B}{List B} (f a) (\x. bind {List B}{List B} r (\xs. return (cons x xs))))));
-- run {List Bit} (mapM {monad=contMonad} (\b. cont (\k. k (not b))) (cons I (cons O nil))) (\x. x)


-- Traversable

let Trav : (U -> U) -> U
    = \(F : U -> U). {T} -> ((traverse : {G : U -> U}{S T}{app : App G} -> (S -> G T) -> F S -> G (F T)) -> F T) -> F T;